load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library")
load("//:koka.bzl", "koka_binary")

# Koka PEG interpreters - hermetic build (no system koka required)
koka_binary(
    name = "e0peg",
    main = "e0peg.koka",
    srcs = ["e0peg.koka", "peg.koka", "pegeval.koka"],
    deps = ["peg.koka", "pegeval.koka"],
    visibility = ["//visibility:public"],
)

koka_binary(
    name = "e1peg",
    main = "e1peg.koka",
    srcs = ["e1peg.koka", "peg.koka", "pegeval.koka"],
    deps = ["peg.koka", "pegeval.koka"],
    data = ["e1.peg"],
    visibility = ["//visibility:public"],
)

koka_binary(
    name = "e2peg",
    main = "e2peg.koka",
    srcs = ["e2peg.koka", "peg.koka", "pegeval.koka"],
    deps = ["peg.koka", "pegeval.koka"],
    data = ["e2.peg"],
    visibility = ["//visibility:public"],
)

koka_binary(
    name = "e3peg",
    main = "e3peg.koka",
    srcs = ["e3peg.koka", "peg.koka"],
    deps = ["peg.koka"],
    data = ["e3.peg"],
    visibility = ["//visibility:public"],
)

# Hand-written Koka interpreter (two-phase: parse to AST, then evaluate)
koka_binary(
    name = "e1_koka",
    main = "e1.koka",
    srcs = ["e1.koka", "e1-types.koka", "e1-parser.koka", "e1-eval.koka"],
    deps = ["e1-types.koka", "e1-parser.koka", "e1-eval.koka"],
    visibility = ["//visibility:public"],
)

# Export grammar files for runtime use
exports_files(glob(["*.peg"]))

# Export Koka files for tests
exports_files(["peg.koka"], visibility = ["//test:__pkg__"])

# Export C++ source files for bench_intbits
exports_files(
    ["e1.cpp", "e1_compile.cpp"],
    visibility = ["//bench:__pkg__"],
)

# Default configuration (INT_BITS=0, LIMB_BITS=64 - bigint)
cc_binary(
    name = "e1",
    srcs = ["e1.cpp"],
    deps = [":e1_hdrs"],
    visibility = ["//visibility:public"],
)

cc_binary(
    name = "e1_compile",
    srcs = ["e1_compile.cpp"],
    deps = [":e1_hdrs"],
    visibility = ["//visibility:public"],
)

cc_library(
    name = "e1_hdrs",
    hdrs = ["e1.hpp", "e1_bigint.hpp", "e1_preamble.hpp"],
    visibility = ["//visibility:public"],
)

# INT_BITS configurations for benchmarking
# Bigint with different LIMB_BITS
[
    cc_binary(
        name = "e1_int0_limb" + limb,
        srcs = ["e1.cpp"],
        local_defines = ["INT_BITS=0", "LIMB_BITS=" + limb],
        deps = [":e1_hdrs"],
        visibility = ["//bench:__pkg__"],
    )
    for limb in ["32", "64", "128"]
]

[
    cc_binary(
        name = "e1_compile_int0_limb" + limb,
        srcs = ["e1_compile.cpp"],
        local_defines = ["INT_BITS=0", "LIMB_BITS=" + limb],
        deps = [":e1_hdrs"],
        visibility = ["//bench:__pkg__"],
    )
    for limb in ["32", "64", "128"]
]

# Fixed-width INT_BITS
[
    cc_binary(
        name = "e1_int" + bits,
        srcs = ["e1.cpp"],
        local_defines = ["INT_BITS=" + bits],
        deps = [":e1_hdrs"],
        visibility = ["//bench:__pkg__"],
    )
    for bits in ["64", "512"]
]

[
    cc_binary(
        name = "e1_compile_int" + bits,
        srcs = ["e1_compile.cpp"],
        local_defines = ["INT_BITS=" + bits],
        deps = [":e1_hdrs"],
        visibility = ["//bench:__pkg__"],
    )
    for bits in ["64", "512"]
]

# Export individual headers for genrules
exports_files(["e1.hpp", "e1_bigint.hpp", "e1_preamble.hpp"])

# Compile bigint runtime to LLVM IR
# Uses toolchains_llvm_bootstrapped clang with libc++ headers from the same toolchain
genrule(
    name = "e1_rt_bigint_ll",
    srcs = ["e1_rt_bigint.cpp", "e1_bigint.hpp"],
    outs = ["e1_rt_bigint.ll"],
    cmd = """
        LIBCXX_HDR=$(execpath @@toolchains_llvm_bootstrapped++http_archive+libcxx//:libcxx_headers_include_search_directory)
        LIBCXXABI_HDR=$(execpath @@toolchains_llvm_bootstrapped++http_archive+libcxxabi//:libcxxabi_headers_include_search_directory)
        GLIBC_HDR=$(execpath @toolchains_llvm_bootstrapped//runtimes/glibc:glibc_headers_include_search_directory)
        KERNEL_HDR=$(execpath @@toolchains_llvm_bootstrapped++kernel_headers+kernel_headers//:kernel_headers_directory)
        $(execpath @toolchains_llvm_bootstrapped//tools:clang) -x c++ -std=c++26 \
            -isystem $$LIBCXX_HDR \
            -isystem $$LIBCXXABI_HDR \
            -isystem $$GLIBC_HDR \
            -isystem $$KERNEL_HDR \
            -Wno-vla-cxx-extension -S -emit-llvm -O3 \
            $(location e1_rt_bigint.cpp) -o $@
    """,
    tools = [
        "@toolchains_llvm_bootstrapped//tools:clang",
        "@@toolchains_llvm_bootstrapped++http_archive+libcxx//:libcxx_headers_include_search_directory",
        "@@toolchains_llvm_bootstrapped++http_archive+libcxxabi//:libcxxabi_headers_include_search_directory",
        "@toolchains_llvm_bootstrapped//runtimes/glibc:glibc_headers_include_search_directory",
        "@@toolchains_llvm_bootstrapped++kernel_headers+kernel_headers//:kernel_headers_directory",
    ],
    local = True,
    visibility = ["//visibility:public"],
)
