// PL/0 Level 0 Grammar (PEG variant)
//
// Sequential only (NOT Turing-complete)
// Has 2 of 3 Minsky machine features:
//   1. Increment/decrement - + and - operations
//   2. Variables - unbounded integer storage (counters)
//   MISSING: Conditional control flow (no loop, no zero-test)

program    = _ statement*
statement  = binding / print_stmt

binding    = id:ident _ ":=" _ e:expression { Assign($id, $e) }
print_stmt = "print" _ e:expression { Print($e) }

expression = sum_expr
sum_expr   = unary sum_tail*
sum_tail   = "+" _ unary { Add }
           / "-" _ unary { Sub }
unary      = "-" _ e:atom { Neg($e) }
           / atom
atom       = int_lit
           / id:ident { Var($id) }
           / "(" _ e:expression ")" _ { $e }

int_lit    = digit+ _ { Int($0) }

keyword    = "print" !idchar
ident      = !keyword letter idchar* _ { Ident($0) }
idchar     = [a-zA-Z0-9_]
letter     = [a-zA-Z]
digit      = [0-9]

_          = ([ \t\n\r] / comment)*
comment    = "//" [^\n]*
